import java.util.concurrent.Callable
import org.opensearch.gradle.testclusters.StandaloneRestIntegTestTask

/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

buildscript {
  ext {
    opensearch_group = "org.opensearch"
    opensearch_version = System.getProperty("opensearch.version", "2.12.0-SNAPSHOT")
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
    buildVersionQualifier = System.getProperty("build.version_qualifier", "")

    bwcVersionShort = "2.11.0"
    bwcVersion = bwcVersionShort + ".0"
      //https://github.com/opensearch-project/custom-codecs/archive/refs/tags/2.11.0.0.zip
//    bwcOpenSearchCustomCodecsDownload = 'https://ci.opensearch.org/ci/dbc/distribution-build-opensearch/' + bwcVersionShort + '/latest/linux/x64/tar/builds/' +
//            'opensearch/plugins/custom-codecs-' + bwcVersion + '.zip'
      //https://repo1.maven.org/maven2/org/opensearch/plugin/opensearch-custom-codecs/2.11.0.0/opensearch-custom-codecs-2.11.0.0.zip
      bwcOpenSearchCustomCodecsDownload = 'https://repo1.maven.org/maven2/org/opensearch/plugin/opensearch-custom-codecs/' + bwcVersion + '/opensearch-custom-codecs-' + bwcVersion + '.zip'
    baseName = "customCodecsBwcCluster"
    bwcFilePath = "src/test/resources/org/opensearch/customcodecs/bwc/"
    bwcCustomCodecsPath = bwcFilePath + "custom-codecs/"
  }

  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "${opensearch_group}.gradle:build-tools:${opensearch_version}"
    classpath "org.jacoco:org.jacoco.agent:0.8.5"
  }
}

plugins {
  id 'java'
}

apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.internal-cluster-test'
apply plugin: 'opensearch.pluginzip'
apply plugin: 'opensearch.rest-test'

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://plugins.gradle.org/m2/" }
  maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
}

allprojects {
  group 'org.opensearch'
  version = opensearch_version.tokenize('-')[0] + '.0'
  if (buildVersionQualifier) {
    version += "-${buildVersionQualifier}"
  }
  if (isSnapshot) {
    version += "-SNAPSHOT"
  }

  plugins.withId('java') {
      targetCompatibility = JavaVersion.VERSION_11
      sourceCompatibility = JavaVersion.VERSION_11
  }
}

opensearchplugin {
  name 'opensearch-custom-codecs'
  description 'OpenSearch plugin that implements custom compression codecs'
  classname 'org.opensearch.index.codec.customcodecs.CustomCodecPlugin'
  licenseFile rootProject.file('LICENSE')
  noticeFile rootProject.file('NOTICE')
}

dependencies {
  api "com.github.luben:zstd-jni:1.5.5-5"
}

allprojects {
    // Default to the apache license
    project.ext.licenseName = 'The Apache Software License, Version 2.0'
    project.ext.licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    publishing {
        repositories {
            maven {
                name = 'staging'
                url = "${rootProject.buildDir}/local-staging-repo"
            }
        }
        publications {
            // add license information to generated poms
            pluginZip(MavenPublication) { publication ->
                pom {
                    name = "opensearch-custom-codecs"
                    description = "OpenSearch plugin that implements custom compression codecs"
                }
                pom.withXml { XmlProvider xml ->
                    Node node = xml.asNode()
                    node.appendNode('inceptionYear', '2021')

                    Node license = node.appendNode('licenses').appendNode('license')
                    license.appendNode('name', project.licenseName)
                    license.appendNode('url', project.licenseUrl)

                    Node developer = node.appendNode('developers').appendNode('developer')
                    developer.appendNode('name', 'OpenSearch')
                    developer.appendNode('url', 'https://github.com/opensearch-project/custom-codecs')
                }
            }
        }
    }
}

publishing {
    publications {
        pluginZip(MavenPublication) { publication ->
            pom {
                name = "opensearch-custom-codecs"
                description = "OpenSearch plugin that implements custom compression codecs"
                groupId = "org.opensearch.plugin"
            }
        }
    }
    repositories {
        maven {
            name = "Snapshots"
            url = "https://aws.oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$System.env.SONATYPE_USERNAME"
                password "$System.env.SONATYPE_PASSWORD"
            }
        }
    }
}

// ignore missing javadocs
tasks.withType(Javadoc).configureEach { Javadoc javadoc ->
  // the -quiet here is because of a bug in gradle, in that adding a string option
  // by itself is not added to the options. By adding quiet, both this option and
  // the "value" -quiet is added, separated by a space. This is ok since the javadoc
  // command already adds -quiet, so we are just duplicating it
  // see https://discuss.gradle.org/t/add-custom-javadoc-option-that-does-not-take-an-argument/5959
  javadoc.options.encoding = 'UTF8'
  javadoc.options.addStringOption('Xdoclint:all,-missing', '-quiet')
  boolean failOnJavadocWarning = project.ext.has('failOnJavadocWarning') ? project.ext.get('failOnJavadocWarning') : true
  if (failOnJavadocWarning) {
    javadoc.options.addStringOption('Xwerror', '-quiet')
  }
  javadoc.options.tags = ["opensearch.internal", "opensearch.api", "opensearch.experimental"]
  javadoc.options.addStringOption("-release", java.targetCompatibility.majorVersion)
}

loggerUsageCheck.enabled = false
validateNebulaPom.enabled = false

sourceSets {
  integTest {
    java {
        srcDirs file("src/integrationTest/java")
    }
    compileClasspath += sourceSets["main"].output + configurations["testRuntimeClasspath"]
    runtimeClasspath += output + compileClasspath
  }
}

tasks.named("testingConventions").configure {
    naming.clear()
    naming {
        Tests {
            baseClass "org.apache.lucene.tests.util.LuceneTestCase"
        }
        IT {
            baseClass "org.opensearch.test.OpenSearchIntegTestCase"
            baseClass "org.opensearch.test.OpenSearchSingleNodeTestCase"
        }
    }
}

integTest {
    description = "Run tests against a cluster"
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath

    dependsOn "bundlePlugin"
    systemProperty 'tests.security.manager', 'true'

    systemProperty "https", System.getProperty("https")
    systemProperty "user", System.getProperty("user")
    systemProperty "password", System.getProperty("password")

    if (System.getProperty("tests.rest.bwcsuite") == null) {
        filter {
            excludeTestsMatching "org.opensearch.index.codecs.customcodecs.bwc.*IT"
        }
    }
}

testClusters.integTest {
    testDistribution = "ARCHIVE"
    plugin(project.tasks.bundlePlugin.archiveFile)
}

2.times {i ->
    testClusters {
        "${baseName}$i" {
            testDistribution = "ARCHIVE"
            versions = [bwcVersionShort, opensearch_version]
            numberOfNodes = 3
            plugin(provider(new Callable<RegularFile>(){
                @Override
                RegularFile call() throws Exception {
                    return new RegularFile() {
                        @Override
                        File getAsFile() {
                            if (new File("$project.rootDir/$bwcFilePath/custom-codecs/$bwcVersion").exists()) {
                                project.delete(files("$project.rootDir/$bwcFilePath/custom-codecs/$bwcVersion"))
                            }
                            project.mkdir bwcCustomCodecsPath + bwcVersion
                            ant.get(src: bwcOpenSearchCustomCodecsDownload,
                                    dest: "$project.rootDir/$bwcFilePath/custom-codecs/$bwcVersion",
                                    httpusecaches: false)
                            return fileTree("$project.rootDir/$bwcFilePath/custom-codecs/$bwcVersion").getSingleFile()
                        }
                    }
                }
            }))
            setting 'path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
            setting 'http.content_type.required', 'true'
        }
    }
}

String opensearchCustomCodecsPlugin = "opensearch-custom-codecs-" + project.version + ".zip"
List<Provider<RegularFile>> plugins = [
        provider(new Callable<RegularFile>() {
            @Override
            RegularFile call() throws Exception {
                return new RegularFile() {
                    @Override
                    File getAsFile() {
                        project.mkdir "$bwcFilePath/$project.version"
                        copy {
                            from "$buildDir/distributions/$opensearchCustomCodecsPlugin"
                            into "$bwcFilePath/$project.version"
                        }
                        return fileTree(bwcFilePath + project.version).getSingleFile()
                    }
                }
            }
        })
]

// Creates 2 test clusters with 3 nodes of the old version.
2.times {i ->
    task "${baseName}#oldVersionClusterTask$i"(type: StandaloneRestIntegTestTask) {
        useCluster testClusters."${baseName}$i"
        filter {
            includeTestsMatching "org.opensearch.index.codec.customcodecs.bwc.*"
        }
        systemProperty 'tests.rest.bwcsuite', 'old_cluster'
        systemProperty 'tests.rest.bwcsuite_round', 'old'
        systemProperty 'tests.plugin_bwc_version', bwcVersion
        nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${baseName}$i".allHttpSocketURI.join(",")}")
        nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${baseName}$i".getName()}")
    }
}

// Upgrades one node of the old cluster to new OpenSearch version with upgraded plugin version
// This results in a mixed cluster with 2 nodes on the old version and 1 upgraded node.
// This is also used as a one third upgraded cluster for a rolling upgrade.
task "${baseName}#mixedClusterTask"(type: StandaloneRestIntegTestTask) {
    useCluster testClusters."${baseName}0"
    dependsOn "${baseName}#oldVersionClusterTask0"
    doFirst {
        testClusters."${baseName}0".upgradeNodeAndPluginToNextVersion(plugins)
    }
    filter {
        includeTestsMatching "org.opensearch.index.codec.customcodecs.bwc.*"
    }
    systemProperty 'tests.rest.bwcsuite', 'mixed_cluster'
    systemProperty 'tests.rest.bwcsuite_round', 'first'
    systemProperty 'tests.plugin_bwc_version', bwcVersion
    nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${baseName}0".allHttpSocketURI.join(",")}")
    nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${baseName}0".getName()}")
}

// Upgrades the second node to new OpenSearch version with upgraded plugin version after the first node is upgraded.
// This results in a mixed cluster with 1 node on the old version and 2 upgraded nodes.
// This is used for rolling upgrade.
task "${baseName}#twoThirdsUpgradedClusterTask"(type: StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#mixedClusterTask"
    useCluster testClusters."${baseName}0"
    doFirst {
        testClusters."${baseName}0".upgradeNodeAndPluginToNextVersion(plugins)
    }
    filter {
        includeTestsMatching "org.opensearch.index.codec.customcodecs.bwc.*"
    }
    systemProperty 'tests.rest.bwcsuite', 'mixed_cluster'
    systemProperty 'tests.rest.bwcsuite_round', 'second'
    systemProperty 'tests.plugin_bwc_version', bwcVersion
    nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${baseName}0".allHttpSocketURI.join(",")}")
    nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${baseName}0".getName()}")
}

// Upgrades the third node to new OpenSearch version with upgraded plugin version after the second node is upgraded.
// This results in a fully upgraded cluster.
// This is used for rolling upgrade.
task "${baseName}#rollingUpgradeClusterTask"(type: StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#twoThirdsUpgradedClusterTask"
    useCluster testClusters."${baseName}0"
    doFirst {
        testClusters."${baseName}0".upgradeNodeAndPluginToNextVersion(plugins)
    }
    filter {
        includeTestsMatching "org.opensearch.index.codec.customcodecs.bwc.*"
    }
    mustRunAfter "${baseName}#mixedClusterTask"
    systemProperty 'tests.rest.bwcsuite', 'mixed_cluster'
    systemProperty 'tests.rest.bwcsuite_round', 'third'
    systemProperty 'tests.plugin_bwc_version', bwcVersion
    nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${baseName}0".allHttpSocketURI.join(",")}")
    nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${baseName}0".getName()}")
}

// Upgrades all the nodes of the old cluster to new OpenSearch version with upgraded plugin version
// at the same time resulting in a fully upgraded cluster.
task "${baseName}#fullRestartClusterTask"(type: StandaloneRestIntegTestTask) {
    dependsOn "${baseName}#oldVersionClusterTask1"
    useCluster testClusters."${baseName}1"
    doFirst {
        testClusters."${baseName}1".upgradeAllNodesAndPluginsToNextVersion(plugins)
    }
    filter {
        includeTestsMatching "org.opensearch.index.codec.customcodecs.bwc.*"
    }
    systemProperty 'tests.rest.bwcsuite', 'upgraded_cluster'
    systemProperty 'tests.plugin_bwc_version', bwcVersion
    nonInputProperties.systemProperty('tests.rest.cluster', "${-> testClusters."${baseName}1".allHttpSocketURI.join(",")}")
    nonInputProperties.systemProperty('tests.clustername', "${-> testClusters."${baseName}1".getName()}")
}

// A bwc test suite which runs all the bwc tasks combined.
task bwcTestSuite(type: StandaloneRestIntegTestTask) {
    exclude '**/*Test*'
    exclude '**/*IT*'
    dependsOn tasks.named("${baseName}#mixedClusterTask")
    dependsOn tasks.named("${baseName}#rollingUpgradeClusterTask")
    dependsOn tasks.named("${baseName}#fullRestartClusterTask")
}

run {
    doFirst {
        // There seems to be an issue when running multi node run or integ tasks with unicast_hosts
        // not being written, the waitForAllConditions ensures it's written
        getClusters().forEach { cluster ->
            cluster.waitForAllConditions()
        }
    }
    useCluster testClusters.integTest
}

tasks.withType(PublishToMavenRepository) {
    def predicate = provider {
        publication.name == "pluginZip"
    }
    onlyIf("Publishing only ZIP distributions") {
        predicate.get()
    }
}

task updateVersion {
    onlyIf { System.getProperty('newVersion') }
    doLast {
        ext.newVersion = System.getProperty('newVersion')
        println "Setting version to ${newVersion}."
        // String tokenization to support -SNAPSHOT
        ant.replaceregexp(file:'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags:'g', byline:true)
    }
}
